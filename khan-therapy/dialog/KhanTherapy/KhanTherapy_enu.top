topic: ~KhanTherapy()
language: enu
include:lexicon_enu.top
include: ../../scripts/butane/dialog/Lexicon/Lexicon_enu.top

# Specific Pepper Store
concept:(r_store) "Pepper Store"
concept:(r_reward) "My Account"
concept:(r_offers) "special offer"

##############  ****** Concepts ******  #################

dynamic: catalog
dynamic: alternates    
dynamic: phones
dynamic: home_theater
dynamic: gaming
dynamic: appliances    
dynamic: samsung_experience_center
dynamic: apple
dynamic: computing
dynamic: customer_service
dynamic: cameras
dynamic: gadgets
dynamic: checkout
dynamic: entrance
dynamic: pepper_self

# Turn off body language
concept:(nbl) [^mode(disabled)]  

##############  ****** HUMAN SAYS ******  #################

concept:(do_you_have) ["{do} you have" "have you got"] {the}
concept:(where_is) ["{"show me" "tell me"} [where wheres where's] {is are} {the}"]
concept:(what_in) ["{"show me" "tell me"} [what whats what's] {"is in"} {the}"]
    
concept:(scan) ["how much is this" "price check" "{can you} scan this" "["are these" "is this"] on sale"]
concept:(map) ["{show "bring up"} {the} store map" "floor plan"]
concept:(search) ["[browse search] {the} {products store inventory}"]    
concept:(loyalty) ["my rewards" "loyalty {card}" "[reward rewards] {zone}" "my account"] #my profile, my account, recognize me
#concept:(daily_deal) ["{"what is"} {the} deal of the day" "{"what is"} {the} daily deal" "{what} deals {"do you have" "are there"}" "special offers" "{"what is" "is anything"} on sale"]    
concept:(daily_deal) ["{"what is"} {the} special [offers offer]" "{"what is" "is anything"} on sale"]    
concept:(go_home) ["home page" "go home" "quit" "stop it" "{i'm "i am" we're "we are"} done"]
concept:(go_back) ["{go} back"]
concept:(section) ["section" "area" "spot" "place"]          

concept:(i_am) ["[I'm i'm "I am" "i am"]" {doing}]
concept:(positive) [well great good awesome fantastic swell peachy keen]
concept:(neutral) [OK okay fine whatever "all right"]
concept:(negative) [shit terrible awful bad disgusting "the worst" sad "a loathesome individual"]

concept:(greeter) greeter mode

##############  ****** ROBOT SAYS ******  #################
concept:(r_map) ^start(boston_animation_library/Stand/rm_click_on_Section) \pau=200\ Click, or tell me the section name for a brief tour. ^wait(boston_animation_library/Stand/rm_click_on_Section)
concept:(r_start) ^start(boston_animation_library/Stand/rm_click_on_Section) \pau=200\ Hello there, how are you doing? ^wait(boston_animation_library/Stand/rm_click_on_Section)
concept:(r_reminiscence) How about we talk about some of your past good memories? 
concept:(r_request) What is your user id?
concept:(r_confirm) Did I hear it right? 
concept:(r_hear) Did I hear it right? 
concept:(r_title) Now tell me: which event will you love to reminisce about?
concept:(r_event_confirm) Is that correct?
concept:(r_more) Is there any other event you want to reminisce about?
concept:(r_search) See what we've got, click or ask me if we have the product you are after.
#Simply type in any product and see what we've got, or just ask me if we have it.
concept:(r_scan) Let's take a look! ^start(boston_animation_library/Stand/rm_qrscanner) For more details on a product, show me its barcode. 
#Just hold the barcode up so I can see it, and I'll get you the details. 
^wait(boston_animation_library/Stand/rm_qrscanner) $KhanTherapy/Scan=ready
#add a feedback (if vocal input)
concept:(r_loyalty) $KhanTherapy/Loyalty=loading [
    "^exist(KhanTherapy/User) Glad to see you $KhanTherapy/User \pau=200\ When you need to log out, just say good bye. #(anim=show_tablet) 
    \pau=500\
    Here is your ~r_offers . $KhanTherapy/Loyalty=ready " 
    "^notExist(KhanTherapy/User) ^start(pepper-store/animations/show_card) To help me recognize you, please show me your card. #anim=show_tablet 
    \pau=200\ You can also sign up by clicking here. ^wait(pepper-store/animations/show_card) $KhanTherapy/Loyalty=ready "  
]
concept:(r_signup) ^run(boston_animation_library/Stand/rm_email_joke) ^start(boston_animation_library/Stand/rm_congratulations) Congratulations $KhanTherapy/Signed=1 "^exist(KhanTherapy/Signee) $KhanTherapy/Signee " , I've sent you an email to confirm your sign up ^wait(boston_animation_library/Stand/rm_congratulations)

####### Product page ########
concept:(r_product_in_stock) "Oh! I love this one, it's a great product!"
concept:(r_product_low_stock) "Oh! I love this product, we have just a few left in stock."
concept:(r_product_out_stock) "Oh! It seems this product is out of stock. Would you like to buy it online?"
concept:(r_buy_online) [
    "^exist(KhanTherapy/User) Alright $KhanTherapy/User ! Please confirm your phone number or you can change it." 
    "^notExist(KhanTherapy/User) Alright! Please enter your phone number on my tablet"
]
concept:(r_sending_link) "^run(pepper-store/animations/send_sms) Perfect, I'm sending you the link"
concept:(r_link_sent) "Ok, I sent you the link. Check your phone!"

concept:(r_daily_deal) ["^exist(KhanTherapy/User) Here's your personalized ~r_offers " "^notExist(KhanTherapy/User) Here's the ~r_offers "] . It's awesome. ^rand["Don't miss this opportunity. " "Get them while they're hot. "]
concept:(r_map_it_q) ^rand[Want "Care for" "Would you like"] me to show you where it is?
concept:(r_prod_deets_q) ^rand[Want "Care" "Would you like"] to see the product details?
concept:(r_show_prod_q) ^rand[Want "Care" "Would you like"] to see the products in that section?



proposal: %start ~r_start
    u1:([~im_good "I am good" "I am feeling good" good ])^rand["Glad to hear" ~happy_human]. Happy to hear that you are doing great. ^gotoReactivate(reminiscence)
    u1:([~im_ok ~i_dont_feel_good "I am sad" sad "{I am feeling}bad" not "Not so good" bad "I am not feeling good"])Oh, you are not feeling as good as I would want you to feel. $KhanTherapy/Request=no

   
proposal: %reminiscence ~r_reminiscence
    u1:(~yes)I will run the reminiscence app now.  ^gotoReactivate(req)
    u1:(no)Oh sorry to hear that. I hope you feel better. 


proposal: %req ~r_request
    u1:(_*) $1 $KhanTherapy/Resident=$1  ^gotoReactivate(confirm)
    

proposal: %confirm ~r_confirm
    u1:(~yes)Great. ^gotoReactivate(title)
    u1:(no) ^gotoReactivate(repeat)

proposal: %hear ~r_hear
    u1:(~yes)Great.
    u1: (no) ^gotoReactivate(repeat)

proposal: %repeat Please repeat your resident id for me?
    u1:(_*) $1 $KhanTherapy/Resident=$1 \pau=2000\ ^gotoReactivate(hear)

proposal: %title ~r_title
    u1:(_*) $1 $KhanTherapy/Event=$1  ^gotoReactivate(event_confirm)

proposal: %event_confirm ~r_event_confirm
    u1:(~yes) Good to know

proposal: %more ~r_more
    u1: (~yes) Okay. ^gotoReactivate(title)
    u1: (no) Well, nice talking to you. Good bye $onStopped=1

    
### Home screen options

proposal: %loyalty ~r_loyalty
proposal: %map ~r_map
proposal: %search ~r_search
proposal: %scan ~r_scan
proposal: %daily_deal ~r_daily_deal



#product page
proposal: %product_in_stock ~r_product_in_stock
proposal: %product_low_stock ~r_product_low_stock
proposal: %product_out_stock ~r_product_out_stock
    u1:(~yes) $KhanTherapy/BuyOnline=1
    u1:(~no) Ok, you can take a look on related on the right product if you want.
proposal: %sending_link ~r_sending_link
proposal: %link_sent ~r_link_sent
    
## Price check
u:(~scan) Price Check. Okay. $KhanTherapy/goto=scan 

## Search/Browse products
u:(~search) Search. Okay. $KhanTherapy/goto=search 

## Map
u:(~map) Store map. Okay. $KhanTherapy/goto=map 

## Loyalty
u:(~loyalty) ~r_reward . Okay. $KhanTherapy/goto=loyalty
u:(e:KhanTherapy/Logout) ["$KhanTherapy/Logout==1 Goodbye $KhanTherapy/User , Come back soon. I alreday miss you. #I hope to see you again soon! 
^clear(KhanTherapy/User)"]

## Daily Deal
u:(~daily_deal) $KhanTherapy/goto=daily_deal Special Offers. Okay. \pau=200\ ~r_daily_deal
    
### sub screen audio tours
proposal: %signup ~r_signup
proposal: %buy_online ~r_buy_online


### Store sections (selected by tablet touch)
        
proposal: %home_theater " ~nbl Home Theater. \PAU=300\ ^start(boston_animation_library/Stand/rm_hometheater) Here you can find T Veez \pau=100\ soundbars \pau=100\ and all your Audio Video Components " ^wait(boston_animation_library/Stand/rm_hometheater)
proposal: %gaming " ~nbl Gaming. \PAU=300\ ^start(boston_animation_library/Stand/rm_gaming) PS4, Xbox one, Wii U and all the latest games " ^wait(boston_animation_library/Stand/rm_gaming)
proposal: %appliances " ~nbl Appliances. \PAU=300\ ^start(boston_animation_library/Stand/rm_appliances) Find all the latest energy efficient appliances for your home. " ^wait(boston_animation_library/Stand/rm_appliances)
proposal: %samsung_experience_center " ~nbl The Samsung Experience Center. \PAU=300\ ^start(boston_animation_library/Stand/rm_samsungexperience) #Check out our Samsung Experience Center to see all the latest Samsung mobile devices and get expert advice. " 
Here, Samsung device experts give you advices about their products. " ^wait(boston_animation_library/Stand/rm_samsungexperience)
proposal: %apple " ~nbl The Apple Boutique. \PAU=300\ ^start(boston_animation_library/Stand/rm_apple) Ipads, Macbooks and all the greatest aksasories to get the most out of your Apple device " ^wait(boston_animation_library/Stand/rm_apple)
proposal: %computing " ~nbl Computing. \PAU=300\ ^start(boston_animation_library/Stand/rm_computing) HP, Lenovo, Dell, Asus and all the greatest brands for all your home and business needs. " ^wait(boston_animation_library/Stand/rm_computing)
proposal: %customer_service " ~nbl Customer service. \PAU=300\ ^start(boston_animation_library/Stand/rm_customerservice) Store Pickup, Returns, and a friendly staff to help answer any #other
 questions you may have." #\Pau=200\ You can also talk to our highly trained Geeksquad members who can help you with any installation or repair needs " 
^wait(boston_animation_library/Stand/rm_customerservice)
proposal: %cameras " ~nbl Cameras. \PAU=300\ ^start(boston_animation_library/Stand/rm_cameras) From point and shoot, to full frame SLRs, the latest, Nikon, Sony, and Cannon cameras here." ^wait(boston_animation_library/Stand/rm_cameras)
proposal: %gadgets " ~nbl Gadgets. \PAU=300\ ^start(boston_animation_library/Stand/rm_gadget) Fitbit, Jawbone, Parrot and many other great lifestyle tech to help you be your best " ^wait(boston_animation_library/Stand/rm_gadget)
proposal: %phones " ~nbl Phones. \PAU=300\ ^start(boston_animation_library/Stand/rm_phones) The latest smart phones and aksasories from Apple, Samsung on all the best carriers " ^wait(boston_animation_library/Stand/rm_phones)
proposal: %checkout " ~nbl Chekout. \PAU=300\ ^start(boston_animation_library/Stand/rm_checkout_01) Fast and efficient place to pay for all your goods " ^wait(boston_animation_library/Stand/rm_checkout_01)
proposal: %entrance  That's the entrance, a magical portal to the technology utopia that is our Store. Remember, you can't leave without buying something. Just kidding.
proposal: %pepper_self  ^start(boston_animation_library/Stand/rm_thatsme) You are here, I am with you.  Wherever we go, there we are. ^wait(boston_animation_library/Stand/rm_thatsme)

### Do you have [exact product in catalog]?

u:({~do_you_have} _[~catalog ~alternates]) Yes! $KhanTherapy/gotoProduct=$1

#u:({~do_you_have} _[~catalog ~alternates]) ^start(boston_animation_library/Stand/rm_lovelyproduct_03) Yes! $KhanTherapy/gotoProduct=$1 The $1 is a lovely product.~r_map_it_q ^wait(boston_animation_library/Stand/rm_lovelyproduct_03)   
#    u1:(~yes) $KhanTherapy/showOnMap=$KhanTherapy/gotoProduct Awesome! Here it is!
#    u1:(~no) ^start(boston_animation_library/Stand/rm_hope_i_can_help) OK. I'll be here if you have any other questions. I'm here to help. ^wait(boston_animation_library/Stand/rm_hope_i_can_help)
#    u1:(~pardon) Apologies. I asked, ~r_map_it_q ^stayInScope
#    u1:([e:Dialog/NotUnderstood e:Dialog/NotUnderstood2 e:Dialog/NotUnderstood3]) ~pardon_r ^stayInScope
#    u1:(e:Dialog/NotUnderstood) ~pardon_r ^stayInScope
#    u1:(e:Dialog/NotUnderstood2) ~not_understood_r ^stayInScope
#    u1:(e:Dialog/NotUnderstood3) ^start(boston_animation_library/Stand/rm_dont_think) You know, \PAU=100\ I'm not sure we're getting anywhere here. I'll page a ~r_store associate to help you. ^wait(boston_animation_library/Stand/rm_dont_think)
    
u:(~where_is _[~catalog ~alternates]) $KhanTherapy/showOnMap=$1 You can find $1 here. ~r_prod_deets_q
    u1:(~yes) $KhanTherapy/gotoProduct=$KhanTherapy/showOnMap Awesome! Here they are!
    u1:(~no) OK. I'll be here if you have any other questions.
    u1:(~pardon) Apologies. I asked, ~r_prod_deets_q ^stayInScope
    u1:(e:Dialog/NotUnderstood) ~pardon_r ^stayInScope
    u1:(e:Dialog/NotUnderstood2) ~not_understood_r ^stayInScope
    u1:(e:Dialog/NotUnderstood3) ^start(boston_animation_library/Stand/rm_dont_think) You know, \PAU=100\ I'm not sure we're getting anywhere here. I'll page a ~r_store associate to help you. ^wait(boston_animation_library/Stand/rm_dont_think)
    
### Do you have/where is [keyword for different sections of the store]?

## phones    
u:(~do_you_have ~phones) Yes! $KhanTherapy/SectionLabel=phones $KhanTherapy/SectionName=Phone ^gotoReactivate(in_section)
u:({~where_is} ~phones) $KhanTherapy/SectionLabel=phones $KhanTherapy/SectionName=Phone ^gotoReactivate(in_section)
u:(~what_in ~phones {~section}) $KhanTherapy/SectionLabel=phones ^gotoReactivate(what_in_section)
        
## home theater    
u:(~do_you_have ~home_theater) Yes! $KhanTherapy/SectionLabel=home_theater $KhanTherapy/SectionName="Home Theater" ^gotoReactivate(in_section)
u:({~where_is} ~home_theater) $KhanTherapy/SectionLabel=home_theater $KhanTherapy/SectionName="Home Theater" ^gotoReactivate(in_section)
u:(~what_in ~home_theater {~section}) $KhanTherapy/SectionLabel=home_theater ^gotoReactivate(what_in_section)
    
## gaming
u:(~do_you_have ~gaming) Yes! $KhanTherapy/SectionLabel=gaming $KhanTherapy/SectionName=Gaming ^gotoReactivate(in_section)
u:({~where_is} ~gaming) $KhanTherapy/SectionLabel=gaming $KhanTherapy/SectionName=Gaming ^gotoReactivate(in_section)
u:(~what_in ~gaming {~section}) $KhanTherapy/SectionLabel=gaming ^gotoReactivate(what_in_section)
        
## appliances    
u:(~do_you_have ~appliances) Yes! $KhanTherapy/SectionLabel=appliances $KhanTherapy/SectionName=Appliance ^gotoReactivate(in_section)
u:({~where_is} ~appliances) $KhanTherapy/SectionLabel=appliances $KhanTherapy/SectionName=Appliance ^gotoReactivate(in_section)
u:(~what_in ~appliances {~section}) $KhanTherapy/SectionLabel=appliances ^gotoReactivate(what_in_section)    
    
## samsung experience center    
u:(~do_you_have ~samsung_experience_center) Yes! $KhanTherapy/SectionLabel=samsung_experience_center $KhanTherapy/SectionName="Samsung Experience Center" ^gotoReactivate(in_section)
u:({~where_is} ~samsung_experience_center) $KhanTherapy/SectionLabel=samsung_experience_center $KhanTherapy/SectionName="Samsung Experience Center" ^gotoReactivate(in_section)
u:(~what_in ~samsung_experience_center {~section}) $KhanTherapy/SectionLabel=samsung_experience_center ^gotoReactivate(what_in_section)
    
## apple    
u:(~do_you_have ~apple) Yes! $KhanTherapy/SectionLabel=apple $KhanTherapy/SectionName=Apple ^gotoReactivate(in_section)
u:({~where_is} ~apple) $KhanTherapy/SectionLabel=apple $KhanTherapy/SectionName=Apple ^gotoReactivate(in_section)
u:(~what_in ~apple {~section}) $KhanTherapy/SectionLabel=apple ^gotoReactivate(what_in_section)
   
## computing    
u:(~do_you_have ~computing) Yes! $KhanTherapy/SectionLabel=computing $KhanTherapy/SectionName=computing ^gotoReactivate(in_section)
u:({~where_is} ~computing) $KhanTherapy/SectionLabel=computing $KhanTherapy/SectionName=Computing ^gotoReactivate(in_section)
u:(~what_in ~computing {~section}) $KhanTherapy/SectionLabel=computing ^gotoReactivate(what_in_section)
    
## cameras    
u:(~do_you_have ~cameras) Yes! $KhanTherapy/SectionLabel=cameras $KhanTherapy/SectionName=Camera ^gotoReactivate(in_section)
u:({~where_is} ~cameras) $KhanTherapy/SectionLabel=cameras $KhanTherapy/SectionName=Camera ^gotoReactivate(in_section)
u:(~what_in ~cameras {~section}) $KhanTherapy/SectionLabel=cameras ^gotoReactivate(what_in_section)
    
## gadgets
u:(~do_you_have ~gadgets) Yes! $KhanTherapy/SectionLabel=gadgets $KhanTherapy/SectionName=Gadget ^gotoReactivate(in_section)
u:({~where_is} ~gadgets) $KhanTherapy/SectionLabel=gadgets $KhanTherapy/SectionName=Gadget ^gotoReactivate(in_section)
u:(~what_in ~gadgets {~section}) $KhanTherapy/SectionLabel=gadgets ^gotoReactivate(what_in_section)

    
proposal: %in_section $KhanTherapy/highlightSection=$KhanTherapy/SectionLabel They are in the ^first["$KhanTherapy/SectionName=="Samsung Experience Center" $KhanTherapy/SectionName" "$KhanTherapy/SectionName section"] . ~r_show_prod_q
    u1:(~yes) $KhanTherapy/showProdBySection=$KhanTherapy/SectionLabel ~great_r
    u1:(~no) OK. I'll be here if you have any other questions.
    u1:(~pardon) Apologies. I asked, ~r_show_prod_q ^stayInScope
    u1:(e:Dialog/NotUnderstood) ~pardon_r ^stayInScope
    u1:(e:Dialog/NotUnderstood2) ~not_understood_r ^stayInScope
    u1:(e:Dialog/NotUnderstood3) ^start(boston_animation_library/Stand/rm_dont_think) I'm having trouble understanding, let me page a ~r_store associate to help. ^wait(boston_animation_lib_ary/Stand/rm_dont_think)
    
proposal: %what_in_section $KhanTherapy/showProdBySection=$KhanTherapy/SectionLabel Check out my tablet for all the products in that section. ~r_map_it_q
    u1:(~yes) $KhanTherapy/highlightSection=$KhanTherapy/SectionLabel ~great_r
    u1:(~no) OK. I'll be here if you have any other questions.
    u1:(~pardon) Apologies. I asked, ~r_map_it_q ^stayInScope
    u1:(e:Dialog/NotUnderstood) ~pardon_r ^stayInScope
    u1:(e:Dialog/NotUnderstood2) ~not_understood_r ^stayInScope
    u1:(e:Dialog/NotUnderstood3) ^start(boston_animation_library/Stand/rm_dont_think) I'm having trouble understanding, let me page a ~r_store associate to help.  ^wait(boston_animation_library/Stand/rm_dont_think) 

                    
### Do you have/where is/what's in [something we don't think we have]?
        
u:(~do_you_have _*) ^start(boston_animation_library/Stand/rm_dont_think) I don't think so! I'm just a robot! Stick around I page a ~r_store associate to help you.
#I don't think so, but I'm not perfect I'll page a ~r_store associate to help you. 
^wait(boston_animation_library/Stand/rm_dont_think)
u:(~where_is _*) ^start(boston_animation_library/Stand/rm_cant_find) Unfortunately, I can't seem to find that product. Stick around I page ~r_store associate to help you.
#I'll page a ~r_store associate to help you. 
^wait(boston_animation_library/Stand/rm_cant_find)       
u:(~what_in _*) ^start(boston_animation_library/Stand/rm_dont_think) ~sorry_r . \PAU=200\ oh! I'm not sure I understand. I'll page a ~r_store associate to help you, they're experts. ^wait(boston_animation_library/Stand/rm_dont_think)
        
### Do you have/where is [location in the store]?
        
u:([~do_you_have ~where_is] ~customer_service) $KhanTherapy/highlightSection=customer_service You can find Customer service right here.            
u:(~where_is ~checkout) $KhanTherapy/highlightSection=checkout The checkout is here.
u:(~where_is ~entrance) $KhanTherapy/highlightSection=entrance The entrance is here.
u:(~where_is ~pepper) ^start(boston_animation_library/Stand/rm_right_here) Me?  I'm right here! ^wait(boston_animation_library/Stand/rm_right_here)

### Random shit
    
u:("~i_am looking for a new computer") ^start(boston_animation_library/Stand/rm_computing) Great, we have all the greatest brands for your home and business needs, like HP, Lenovo, Dell, Asus and Apple. ^wait(boston_animation_library/Stand/rm_computing)
u:("{my} computer is broken") ^start(boston_animation_library/Stand/rm_thats_not_good) That's not good. Our highly trained customer service can help you with any repair needs. ^wait(boston_animation_library/Stand/rm_thats_not_good)
u:("{"i would like to" "i'd like to" "can I"} make a return") ^start(boston_animation_library/Stand/rm_you_can_speak) That's not a problem, you can speak with our customer service agents near checkout to make any returns. ^wait(boston_animation_library/Stand/rm_you_can_speak)
u:("{"i would like to" "i'd like to" "can I"} ["do an online order pick up" "pick up an online order"]") ^start(boston_animation_library/Stand/rm_you_can_speak) You can speak with our customer service agents near checkout to pick up your order. ^wait(boston_animation_library/Stand/rm_you_can_speak)

### Greeter
u:(~greeter) Ok, greeter mode ! $KhanTherapy/greeterMode=1
                        
### Fallback stuff

u:(~go_back) $KhanTherapy/goBack=1 Sure thing.            
u:(~go_home) $KhanTherapy/goto=main You got it.



u:(~how_are_you {Pepper robot}) ^start(boston_animation_library/Stand/rm_im_great) I'm ~great_r ! How are you? ^wait(boston_animation_library/Stand/rm_im_great)
    u1:({~i_am} ~positive) That's ~very_r ~great_r
    u1:({~i_am} [~neutral ~negative]) ^start(boston_animation_library/Stand/rm_hope_i_can_help) Well, I hope I can help give you a ~great_r ~r_store experience. ^wait(boston_animation_library/Stand/rm_hope_i_can_help)
    u1:(~pardon) Apologies. I asked, How are you? ^stayInScope
    u1:(e:Dialog/NotUnderstood) Well, I look forward to helping you today.

   
u:(["how old are you" "["what is" what's] your age"]) I'm 3! I'm still pretty young!

u:(["~what_is your name"
    "how [do should can] I call you"
    "["do you know" "tell me"] {what} your name {is}"
    "tell me who you are"
    "give me your name"
    "what do they call you"]) My name is Pepper.
    
u:(~thank_you {~robot}) ^start(boston_animation_library/Stand/rm_your_welcome) You're welcome. I'll be here if you have any other questions. I'm here to help. ^wait(boston_animation_library/Stand/rm_your_welcome)

### Goodbye / "fistbump moment (was high-five)"

concept:(warning) ["don't leave me hanging" "what are you waiting for?"]
concept:(failure) ["No worries, I hope you had a ~great_r ~r_store experience. Come back soon!"]
concept:(success) ^run(daps/animations/boom_sound) "^rand[great fantastic wonderful super awesome radical cool fabulous groovy "super duper" marvelous] . I hope you had a ~great_r ~r_store experience. Come back soon! "
concept:(highfive_init) ["Say. How about a fist-bump before you go?"] # ["up high" "up top"]
concept:(do_highfive) ^rand["^pCall(Daps.dap('rfistbump', 3.5, 8))" "^pCall(Daps.dap('lfistbump', 3.5, 8))"]
concept:(prepare_safety_daps) $KhanTherapy/safetyOff=1
concept:(restore_safety_daps) $KhanTherapy/safetyOn=1
            
u:([goodbye "good bye" "see [you ya] {later}" farewell] {Pepper robot}) $KhanTherapy/Logout=0 Farewell ["^exist(KhanTherapy/User) $KhanTherapy/User . ^clear(KhanTherapy/User)" "^notExist(KhanTherapy/User) My favorite customer. "] \PAU=400\ ^first["^exist(KhanTherapy/DapsInstalled) ^gotoReactivate(highfive)" " \PAU=10\ "] $KhanTherapy/goto=main

proposal: %highfive ~prepare_safety_daps "^mode(disabled) ~highfive_init ~do_highfive"
    u1:(e:Daps/Failure) "^mode(disabled) ~failure \PAU=2000\ ~restore_safety_daps" ^stayInScope
    u1:(e:Daps/Success) "^mode(disabled) ~success \PAU=2000\ ~restore_safety_daps" ^stayInScope
    u1:(e:Daps/Warning) "^mode(disabled) ~warning" ^stayInScope
    u1:("$Daps/InProgress<>0 *") ^stayInScope

# surmatch            
recover:
u:(~greetings {Pepper robot}) " ~nbl ~greetings_r " ["^exist(KhanTherapy/User) $KhanTherapy/User " "^notExist(KhanTherapy/User) \PAU=100\ "] . Let me know if I can help you with anything.
